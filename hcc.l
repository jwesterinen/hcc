%{
/*
 *	hcc -- hack C compiler lexical analysis
 */

#include "y.tab.h"
#include "error.h"
#include "symtab.h"
%}

letter          [a-zA-Z_]
digit           [0-9]
letter_or_digit [a-zA-Z_0-9]
newline         [\n]
blank           [ \t]
other           .

%%

^"#"{blank}*{digit}+({blank}+.*)?\n	    yymark();

"break"                     return BREAK;
"continue"                  return CONTINUE;
"else"                      return ELSE;
"if"                        return IF;
"int"                       return INT;
"return"                    return RETURN;
"while"                     return WHILE;

">="                        return GE;
"<="                        return LE;
"=="                        return EQ;
"!="                        return NE;
"+="                        return PE;
"-="                        return ME;
"*="                        return TE;
"/="                        return DE;
"%="                        return RE;
"++"                        return PP;
"--"                        return MM;

{letter}{letter_or_digit}*  {
                                s_lookup(Identifier);
                                return Identifier;
                            }

{digit}+                    {
                                s_lookup(Constant);
                                return Constant;
                            }

{newline}                   yylineno++;
{blank}+                    ;

{other}                     return yytext[0];

